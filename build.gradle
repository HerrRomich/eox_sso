/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

import aQute.bnd.build.Workspace
import aQute.bnd.osgi.Constants

/* Add bnd gradle plugin as a script dependency */
buildscript {
  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    } 
  }
  dependencies {
    classpath bndPlugin
    classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
  }
}

plugins {
  id "com.moowork.gulp" version "1.1.1"
}

/* Initialize the bnd workspace */
Workspace.setDriver(Constants.BNDDRIVER_GRADLE)
Workspace.addGestalt(Constants.GESTALT_BATCH, null)
ext.bndWorkspace = new Workspace(rootDir, bnd_cnf).setOffline(gradle.startParameter.offline)

ext.cnf = rootProject.project(bnd_cnf)

/* Configure the subprojects */
subprojects {
  def bndProject = bndWorkspace.getProject(name)

  if (bndProject != null) {

    plugins.apply 'biz.aQute.bnd'
    plugins.apply 'com.moowork.gulp'
  
    task gulpBuildWithOpts(type: GulpTask) {
      doFirst { task ->
        task.project.node {
          nodeModulesDir = file("..")
        }
        task.project.gulp {
          workDir = file("..")
        } 
        args = ["build", "--module=${task.project.name}"]
      }
    }

    processResources.dependsOn 'gulpBuildWithOpts'
  }
}

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}



